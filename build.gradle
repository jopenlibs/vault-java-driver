plugins {
    id 'java-library'
    id 'signing'
    id 'checkstyle'
    id 'maven-publish'
}


group 'io.github.jopenlibs'
archivesBaseName = 'vault-java-driver'
version '5.4.0'

// This project is actually limited to Java 8 compatibility.  See below.
sourceCompatibility = 9
targetCompatibility = 9

repositories {
    mavenCentral()
}

dependencies {
    testImplementation('junit:junit:4.13.2')
    testImplementation('org.mockito:mockito-core:4.8.0')
    testImplementation('org.testcontainers:testcontainers:1.17.5')
    testImplementation('org.eclipse.jetty:jetty-server:11.0.14')
    testImplementation('org.slf4j:slf4j-api:2.0.3')
    testImplementation('org.bouncycastle:bcprov-jdk15on:1.70')
    testImplementation('org.bouncycastle:bcpkix-jdk15on:1.70')
    testImplementation('org.apache.commons:commons-io:1.3.2')

    testRuntimeOnly('org.slf4j:slf4j-simple:2.0.3')
}

// Beginning of Java 9 compatibility config
//
// Allowing a library to support Java 9+ modularity, while also maintaining backwards-compatibility for Java 8 users, is WAY more
// tricky than expected!  The lines below are adapted from a blog article (https://dzone.com/articles/building-java-6-8-libraries-for-jpms-in-gradle),
// and cause the built classes to support a Java 8 JRE, while also including a module definition suitable for use with Java 9.  There are a
// few considerations that come with this:
//
// * You now need JDK 9 or higher to BUILD this library.  You can still USE the built artifact as a dependency in a Java 8 project.
// * Although "sourceCompatibility" and "targetCompatability" above are set for Java 9, the "compileJava" settings below will not
//   allow you to build with any code changes that are not Java 8 compatible.
// * Unfortunately, IntelliJ (and perhaps other IDE's?) will show syntax highlighting, code completion tips, etc for Java 9.  Sorry for
//   the inconvenience.  In any case, you should not commit changes to source control without confirming that the project builds.
compileJava {
    exclude 'module-info.java'

    options.compilerArgs = ['--release', '8']
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

tasks.register('compileModuleInfoJava', JavaCompile) {
    classpath = files()
    source = 'src/main/java/module-info.java'
    destinationDir = compileJava.destinationDir
    options.encoding = compileJava.options.encoding

    doFirst {
        options.compilerArgs = [
                '--release', '9',
                '--module-path', compileJava.classpath.asPath,
        ]
    }
}

compileModuleInfoJava.dependsOn compileJava
classes.dependsOn compileModuleInfoJava
// End of Java 9 compatibility config

tasks.register('javadocJar', Jar) {
    dependsOn tasks.named("javadoc")
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

tasks.register('sourcesJar', Jar) {
    dependsOn tasks.named("classes")
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

//
// Separate unit tests from integration tests.  See: `src/test-integration/README.md`
//
sourceSets {
    unitTests {
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
        java.srcDir file('src/test/java')
    }
    integrationTests {
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
        java.srcDir file('src/test-integration/java')
        resources.srcDirs += file('src/test-integration/resources')
    }
}

configurations {
    unitTestsImplementation.extendsFrom testImplementation
    unitTestsRuntimeOnly.extendsFrom testRuntimeOnly
    integrationTestsImplementation.extendsFrom testImplementation
    integrationTestsRuntimeOnly.extendsFrom testRuntimeOnly
}

tasks.named('test') {
    useJUnit()
    testClassesDirs = sourceSets.unitTests.output.classesDirs
    classpath = sourceSets.unitTests.runtimeClasspath
    testLogging {
        events "passed", "skipped", "failed"
    }

    reports.html.enabled = false
    reports.junitXml.enabled = true
}

def integrationTestTask = tasks.register('integrationTest', Test) {
    useJUnit()
    testClassesDirs = sourceSets.integrationTests.output.classesDirs
    classpath = sourceSets.integrationTests.runtimeClasspath
    testLogging {
        events "passed", "skipped", "failed"
    }

    reports.html.enabled = false
    reports.junitXml.enabled = true
}

//
// Deploying releases to Maven Central (or snapshots to a local Nexus repository).
//
// Snapshots are not signed... but signing the release artifact requires the following project properties:
//
// signing.keyId = <ID of the private key in your secure keyring used for signing JAR's>
// signing.password = <public key password>
// signing.secretKeyRingFile = <full path to your keyring file (i.e secring.gpg)>
//
if (!hasProperty('ossrhUsername')) {
    ext.ossrhUsername = ''
}
if (!hasProperty('ossrhPassword')) {
    ext.ossrhPassword = ''
}

artifacts {
    archives javadocJar, sourcesJar
}

if (hasProperty("publish")) {

    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId = 'vault-java-driver'
                from components.java

                pom {
                    name = 'vault-java-driver'
                    packaging = 'jar'
                    description = 'Zero-dependency Java client for HashiCorp\'s Vault'
                    url = 'https://github.com/jopenlibs/vault-java-driver'

                    scm {
                        connection = 'https://github.com/jopenlibs/vault-java-driver.git'
                        developerConnection = 'https://github.com/jopenlibs/vault-java-driver.git'
                        url = 'https://github.com/jopenlibs/vault-java-driver'
                    }

                    licenses {
                        license {
                            name = 'MIT'
                            url = 'https://github.com/jopenlibs/vault-java-driver/blob/master/README.md#license'
                        }
                    }

                    developers {
                        [
                                developer {
                                    id = 'steve-perkins'
                                    name = 'Steve Perkins'
                                    email = 'steve@steveperkins.com'
                                },
                                developer {
                                    id = 'steve-perkins-bc'
                                    name = 'Steve Perkins'
                                    email = 'steve.perkins@bettercloud.com'
                                },
                                developer {
                                    id = 'jarrodcodes'
                                    name = 'Jarrod Young'
                                    email = 'jarrodsy@gmail.com'
                                },
                                developer {
                                    id = 'tledkov'
                                    name = 'Taras Ledkov'
                                    email = 'tledkov@apache.org'
                                },
                                developer {
                                    id = 'henryx'
                                    name = 'Enrico Bianchi'
                                    email = 'enrico.bianchi@gmail.com'
                                }
                        ]
                    }
                }
            }
        }
        repositories {
            maven {
                credentials {
                    username "$ossrhUsername"
                    password "$ossrhPassword"
                }
                def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            }
        }
    }

    signing {
        sign publishing.publications.mavenJava
    }

    javadoc {
        if (JavaVersion.current().isJava9Compatible()) {
            options.addBooleanOption('html5', true)
        }
    }
}
